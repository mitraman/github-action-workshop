provider "aws" {
  region = var.aws_region
}

data "aws_availability_zones" "available" {
  state = "available"
}

#
# VPC Resources
#  * VPC
#  * Subnets
#  * Internet Gateway
#  * Route Table
#  * NAT
#

resource "aws_vpc" "main" {
  cidr_block = var.main_vpc_cidr

  tags = {
    "Name" = var.vpc_name,
    "kubernetes.io/cluster/${var.cluster-name}" =  "shared",
  }
}

resource "aws_subnet" "public-subnet-a" {
  vpc_id     = aws_vpc.main.id
  cidr_block = var.public_subnet_a_cidr
  availability_zone = data.aws_availability_zones.available.names[0]

  tags = {
      "Name"                                      = "ms-up-running"
     "kubernetes.io/cluster/${var.cluster-name}" = "shared"
     "kubernetes.io/role/elb"                    = "1"
   }
}

resource "aws_subnet" "public-subnet-b" {
  vpc_id     = aws_vpc.main.id
  cidr_block = var.public_subnet_b_cidr  
  availability_zone = data.aws_availability_zones.available.names[1]

  tags = {
     "Name"                                      = "ms-up-running"
     "kubernetes.io/cluster/${var.cluster-name}" = "shared"
     "kubernetes.io/role/elb"           = "1"
   }
}

resource "aws_subnet" "private-subnet-a" {
  vpc_id     =  aws_vpc.main.id
  cidr_block = var.private_subnet_a_cidr
  availability_zone = data.aws_availability_zones.available.names[0]

  tags = {
     "Name"                                      = "ms-up-running"
     "kubernetes.io/cluster/${var.cluster-name}" = "shared"
     "kubernetes.io/role/internal-elb"           = "1"
   }
}

resource "aws_subnet" "private-subnet-b" {
  vpc_id     =  aws_vpc.main.id
  cidr_block = var.private_subnet_b_cidr
  availability_zone = data.aws_availability_zones.available.names[1]

  tags = {
     "Name"                                      = "ms-up-running"
     "kubernetes.io/cluster/${var.cluster-name}" = "shared"
     "kubernetes.io/role/internal-elb"           = "1"
   }
}

resource "aws_internet_gateway" "igw" {
  vpc_id = aws_vpc.main.id

  tags = {
    Name = "ms-up-running"
  }
}

resource "aws_route_table" "public-route" {
  vpc_id = aws_vpc.main.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.igw.id
  }

  tags = {
     "Name" = "ms-up-running"
  }
}

resource "aws_route_table_association" "public-a-association" {
    subnet_id = aws_subnet.public-subnet-a.id
    route_table_id = aws_route_table.public-route.id
}

resource "aws_route_table_association" "public-b-association" {
    subnet_id = aws_subnet.public-subnet-b.id
    route_table_id = aws_route_table.public-route.id
}


// Get two EIPs for the NATs
resource "aws_eip" "nat-a" {
    vpc = true
    tags = {
     "Name" = "ms-up-running"
    }
}

resource "aws_eip" "nat-b" {
    vpc = true    
    tags = {
      "Name" = "ms-up-running"
    }
}

resource "aws_nat_gateway" "nat-gw-a" {
  allocation_id = aws_eip.nat-a.id
  subnet_id     = aws_subnet.public-subnet-a.id
  depends_on = [aws_internet_gateway.igw]

  tags = {
     "Name" = "ms-up-running"
  }
}

resource "aws_nat_gateway" "nat-gw-b" {
  allocation_id = aws_eip.nat-b.id
  subnet_id     = aws_subnet.public-subnet-b.id
  depends_on = [aws_internet_gateway.igw]

  tags = {
     "Name" = "ms-up-running"
  }
}

resource "aws_route_table" "private-route-a" {
  vpc_id = aws_vpc.main.id

  route {
    cidr_block = "0.0.0.0/0"
    nat_gateway_id = aws_nat_gateway.nat-gw-a.id
  }

  tags = {
     "Name" = "ms-up-running"
  }
}

resource "aws_route_table" "private-route-b" {
  vpc_id = aws_vpc.main.id

  route {
    cidr_block = "0.0.0.0/0"
    nat_gateway_id = aws_nat_gateway.nat-gw-b.id
  }

  tags = {
     "Name" = "ms-up-running"
  }
}

resource "aws_route_table_association" "private-a-association" {
    subnet_id = aws_subnet.private-subnet-a.id
    route_table_id = aws_route_table.private-route-a.id
}

resource "aws_route_table_association" "private-b-association" {
    subnet_id = aws_subnet.private-subnet-b.id
    route_table_id = aws_route_table.private-route-b.id
}
